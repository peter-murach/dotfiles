set nocompatible

""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" PLUGINS {{{1
""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
call plug#begin('~/.vim/bundle')

" Searching {{{2
Plug 'preservim/nerdtree'

" Editing {{{2
Plug 'AndrewRadev/splitjoin.vim'
Plug 'msanders/snipmate.vim'
Plug 'PeterRincker/vim-argumentative'
Plug 'tpope/vim-commentary'
Plug 'tpope/vim-endwise'
Plug 'tpope/vim-ragtag'
Plug 'tpope/vim-repeat'
Plug 'tpope/vim-surround'

" Display {{{2
Plug 'scrooloose/syntastic'
Plug 'vim-airline/vim-airline'
Plug 'vim-airline/vim-airline-themes'
Plug 'vim-scripts/taglist.vim'

" Ruby {{{2
Plug 'tpope/vim-bundler'
Plug 'tpope/vim-rails'
Plug 'tpope/vim-rake'
Plug 'vim-ruby/vim-ruby'

" Syntax {{{2
Plug 'ekalinin/Dockerfile.vim'
Plug 'elixir-lang/vim-elixir'
Plug 'elzr/vim-json'
Plug 'kchmck/vim-coffee-script'
Plug 'pangloss/vim-javascript'
Plug 'rhysd/vim-crystal'
Plug 'tpope/vim-cucumber'
Plug 'tpope/vim-markdown'
Plug 'othree/html5.vim'

" Terminal {{{2
Plug 'jgdavey/vim-turbux'
Plug 'tpope/vim-dispatch'
Plug 'tpope/vim-eunuch'
Plug 'tpope/vim-fugitive'

call plug#end()

"""""""""""""""""""
" System-wide setup
"""""""""""""""""""
set noerrorbells     " No beeps
set number           " Show line numbers.
set numberwidth=4
set showmode         " Show what mode we are currently in
set showcmd          " Show what is being typed
set ruler            " Show cursor position.
set scrolloff=3      " Minimum lines to keep around cursor
set shiftwidth=2     " I like 2 spaces for indenting
set tabstop=2        " Set 2 stops
set softtabstop=2    " Set 2 stops
set expandtab        " Spaces instead of tabs
set autoindent       " Always set autoindenting on
set wrap             " Turn on line wrapping.
set visualbell       " No beeping.
set listchars=tab:»·,trail:· " Enable list/listchars
set showbreak=↪      " Mark wrapped lines
set list
set incsearch        " Highlight matches as you type.
set smartcase        " Ignore cases when searching
set showmatch        " Show matching parenthesis
set hlsearch         " Show all matches
set lazyredraw       " Do not redraw while running macros
set enc=utf-8        " Enable unicode support
set history=1000     " Remember more command and search history
set undolevels=1000
set hidden           " Hide buffers instead of closing them
set title            " Change the terminal's title
set ttimeoutlen=50   " Make Esc work faster
set statusline=[%n]\ %<%.99f\ %h%w%m%r%y%*%=%-14.(%l,%c%V%)\ %P
set laststatus=2     " Always show status line
set cursorline       " Highlight the current line
set shiftround       " Round spaces motions
set noesckeys        " Get rid of the delay when hitting esc
set nrformats=       " Treat numerals as decimal
set synmaxcol=800    " Don't try to highlight lines longer than 800 chars
set encoding=utf-8   " Set encoding to utf-8
set nobackup         " Stop saving backups
set nowritebackup
set noswapfile
if has('persistent_undo') " keep undo history across sessions
  set undodir=~/.vimundo
  set undofile
else
  call system('mkdir ~/.vimundo')
endif

" Completion
set wildmenu         " Make tab completion for files/buffers act like bash
set wildmode=list:longest,full "Show a list when pressing tab, and complete first match
set wildignore+=.git,.svn                     " Version control
set wildignore+=*.zip,*.tar                   " Compressed files
set wildignore+=*.avi,*.wmv,*.ogg,*.mp3,*.mov " Video files
set wildignore+=*.jpg,*.gif,*.png             " Binary images
set wildignore+=*.sw?                         " Vim swap files
set completeopt=menu,menuone,longest,preview  " Completion options, menu: popup menu

" Folding
set nofoldenable      " Turn off folding
set foldmethod=syntax " Usefull options: indent, syntax, marker, expr
set foldcolumn=1      " Creates a smalll left-hand gutter for displaying fold info
set foldlevel=100
hi Folded cterm=bold ctermfg=grey ctermbg=none
hi FoldColumn cterm=bold ctermfg=grey ctermbg=none

augroup cline
  au!
  au WinLeave,InsertEnter * set nocursorline
  au WinEnter,InsertLeave * set cursorline
augroup END

syntax enable        " Turn on syntax highlightning
" Pick current color scheme
set background=dark
colorscheme Tomorrow-Night
let g:airline_theme="tomorrow"

"""""""""""""""""""""""""
" vim-ruby configuration
"""""""""""""""""""""""""
:let g:ruby_indent_assignment_style = "hanging"
:let g:ruby_indent_block_style = "do"

""""""""""""
" Mappings
""""""""""""
let mapleader=","    " Change the mapleader from \ to ,

" Mappings for often mistyped :w as :W
nmap :W :w
nmap :W! :w!
nmap :Q! :q!
nmap :Wq! :wq!
nmap :WQ! :wq!
nmap k gk
nmap j gj
nnoremap <leader>p :set paste<CR>o<esc>"*]p:set nopaste<cr>

" Easy list traversal
nnoremap <silent> [b :bprevious<cr>
nnoremap <silent> ]b :bnext<cr>
nnoremap <silent> [B :bfirst<cr>
nnoremap <silent> ]B :blast<cr>

" Editing
nnoremap <leader>ev :vsplit $MYVIMRC<cr>
nnoremap <leader>sv :source $MYVIMRC<cr>

" Tab mappings
map <leader>tt :tabnew<cr>
map <leader>te :tabedit
map <leader>tc :tabclose<cr>
map <leader>to :tabonly<cr>
map <leader>tn :tabnext<cr>
map <leader>tp :tabprevious<cr>
map <leader>tf :tabfirst<cr>
map <leader>tl :tablast<cr>
map <leader>tm :tabmove
map <leader>[  :tabprevious<cr>
map <leader>]  :tabnext<cr>

" Shortcuts for Rails commands
map <leader>rc  :Rcontroller<space>
map <leader>rf  :Rfixtures<space>
map <leader>rh  :Rhelper<space>
map <leader>ri  :Rinitializer<space>
map <leader>rit :Rintegrationtest<space>
map <leader>rj  :Rjavascript<space>
map <leader>rl  :Rlayout<space>
map <leader>rlo :Rlocale<space>
map <leader>rmi :Rmigration<space>
map <leader>rm  :Rmodel<space>
map <leader>rs  :Rspec<space>
map <leader>ry  :Rstylesheet<space>
map <leader>ru  :Runittest<space>
map <leader>rv  :Rview<space>

map <leader>sc :sp db/schema.rb<cr>

" NERD_tree.vim
nnoremap <leader>n :NERDTreeFocus<cr>
nnoremap <C-n> :NERDTree<cr>
nnoremap <C-t> :NERDTreeToggle<cr>

map <leader>o :call RunCurrentLineInTest()<cr>
map <leader>y :call RunCurrentTest()<cr>
map <leader>u :call RunAllTests()<cr>

" Shortcuts for git
map <leader>ga :Gadd<cr>
map <leader>gb :Gblame<cr>
map <leader>gc :Gcommit -m ""<left>
map <leader>gd :Gdiff<cr>
map <leader>gm :Gmove<cr>
map <leader>gr :Gremove<cr>
map <leader>gs :Gstatus<cr>
map <leader>gw :Gwrite<cr>
vmap <leader>b :<C-U>!git blame <C-R>=expand("%:p") <CR> \| sed -n <C-R>=line("'<") <CR>,<C-R>=line("'>") <CR>p <CR>

" Begin searching with ack
map <leader>f :Ack<space>

" No more accidents with Help
nmap <F1> <Esc>
map! <F1> <Esc>

cnoremap %% <C-R>=expand('%:h').'/'<cr>
map <leader>e  :e %%
map <leader>es :sp %%
map <leader>ev :vsp %%
map <leader>et :tabe %%
map <leader>v  :view %%

" Auto commands
if has("autocmd")
  " Include these in an autocmd group for easy delition
  augroup vimrcEx
    " Jump to last cursor position
    autocmd BufReadPost *
            \ if line("'\"") > 0 && line("'\"") <= line("$") |
            \   exe "normal g`\"" |
            \ endif
    " Leave return key alone when in command line mode
    autocmd! CmdWinEnter * :unmap <cr>
    autocmd! CmdWinLeave * :call MapCR()
  augroup END

  augroup myfiletypes
    " treat rackup files like ruby
    au BufRead,BufNewFile *.ru set ft=ruby
    au BufRead,BufNewFile Gemfile set ft=ruby
  augroup END

  "Spellcheck for md and txt files
  autocmd FileType markdown setlocal spell
  autocmd FileType text setlocal spell
endif

""""""""""
" Plugins
""""""""""

" ================== Vim-go ====================
let g:go_disable_autoinstall=1
let g:go_fmt_fail_silently=1

au FileType go nmap gd <Plug>(go-def)
au FileType go nmap <leader>s <Plug>(go-def-split)
au FileType go nmap <leader>v <Plug>(go-def-vertical)

au FileType go nmap <leader>r <Plug>(go-run)
au FileType go nmap <leader>b <Plug>(go-run)
au FileType go nmap <leader>t <Plug>(go-run)

""""""""""
" Scripts
""""""""""

""""""""""""""""""""""""""""""""""
" Add frozen_string_literal: true
""""""""""""""""""""""""""""""""""
function! AddFrozenStringLiteral()
  let l:literal = "# frozen_string_literal: true"
  let l:filename = expand("%")

  if filename =~# "\.rb$"
    call append(0, literal)
    call append(1, "")
  endif
endfunction
map <leader>ff :call AddFrozenStringLiteral()<cr>
autocmd BufNewFile *.rb call AddFrozenStringLiteral()

""""""""""""""""""""""""""""""""""""""""""""""
" Clear the search buffer when hitting return
""""""""""""""""""""""""""""""""""""""""""""""
function! MapCR()
  nnoremap <CR> :nohlsearch<cr>
endfunction
call MapCR()

"""""""""""
" Run tests
""""""""""""
function! InTestFile()
  return match(expand("%"), '\(.feature\|_spec.rb\|_test.rb\)$') != -1
endfunction

function! SetLastTestCommand(test)
  let s:last_test_cmd = a:test
endfunction

function! RunAllTests()
  let l:test = ''
  call SetLastTestCommand(l:test)
  call RunTests(l:test)
endfunction

function! RunCurrentTest()
  if InTestFile()
    let l:test = @%
    call SetLastTestCommand(l:test)
    call RunTests(l:test)
  else
    call RunLastTest()
  endif
endfunction

function! RunCurrentLineInTest()
  let l:test = @% . ":" . line(".")
  call SetLastTestCommand(l:test)
  call RunTests(l:test)
endfunction

function! RunLastTest()
  if exists("s:last_test_cmd")
    call RunTests(s:last_test_cmd)
  endif
endfunction

function! RunTests(filename)
  if !empty(expand("%"))
    write
  endif
  exec "!" . RunWithBundler() . CorrectTestRunner() . " " . a:filename
endfunction

function! RunWithBundler()
  if filereadable('Gemfile')
    return "bundle exec "
  else
    return ""
  endif
endfunction

function! CorrectTestRunner()
  if expand('%') =~# '\.feature$'
    return "cucumber -r features"
  elseif expand('%') =~# '_spec\.rb$'
    return "rspec"
  else
    return "ruby -I test"
  endif
endfunction
